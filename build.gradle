buildscript {
    ext {
        kotlin_version = '1.2.21'
        spring_boot_version = '1.5.10.RELEASE'
        querydsl_version = '4.1.4'
        flyway_version = '5.0.7'
        h2_version = '1.4.196'
        yaml_version = '1.17'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"//기본은 final이다. open으로 변경한다.
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"// 해당 클래스들의 기본 생성자(Arguments가 없는것)을 생성한다.
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "com.h2database:h2:$h2_version"
        classpath "gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:$flyway_version"
        classpath "org.yaml:snakeyaml:$yaml_version"
    }
}

/** plugins */
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.flywaydb.flyway'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // require
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "com.querydsl:querydsl-core:$querydsl_version"
    compile "com.querydsl:querydsl-jpa:$querydsl_version"
    kapt "com.querydsl:querydsl-apt:$querydsl_version:jpa"// 끝에 :jpa로 끝난다.
    compile "org.flywaydb:flyway-core:$flyway_version"
    runtime "com.h2database:h2"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

/**
 * Spring.profiles.active
 * 프로파일이 존재하면 application-{profile}.yml을 로딩한다.
 * 프로파일이 존재하지 않는다면 application.yml을 로딩한다.
 */
final SPRING_CONFIG = {
    final PROFILE  = System.getProperty("spring.profiles.active")
    println "-Dspring.profiles.active='${PROFILE}'"

    def spring_config
    if (PROFILE) {
        spring_config = new org.yaml.snakeyaml.Yaml().load( new File("src/main/resources/application-${PROFILE}.yml").newInputStream())
    } else {
        spring_config = new org.yaml.snakeyaml.Yaml().load( new File("src/main/resources/application.yml").newInputStream())
    }

    return spring_config
}()

group = 'd2'
version = '0.0.3'
buildDir = 'out'
mainClassName = 'd2.ApplicationKt'

/** Language Level */
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

jar {
    manifest {
        attributes  'Implementation-Title': 'Kotlin for springboot', 'Implementation-Version': version, 'Main-Class': mainClassName
    }
    dependsOn configurations.runtime
    from {
        configurations.compile.collect {it.isDirectory()? it: zipTree(it)}
    }
}

/** Kotlin Target platform */
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
    main.resources.srcDirs += 'src/main/resources'
}

idea {
    module {
        def kaptMain = file('build/generated/source/kapt/main')
        sourceDirs += kaptMain
        generatedSourceDirs += kaptMain
    }
}

springBoot  {
    buildInfo()
}

flyway {
    url = SPRING_CONFIG.spring.datasource.url
    user = SPRING_CONFIG.flyway.user
}